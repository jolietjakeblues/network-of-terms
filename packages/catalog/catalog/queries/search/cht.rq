PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri ;
        skos:related ?related_uri ;
        skos:exactMatch ?exactMatch_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    ?related_uri skos:prefLabel ?related_prefLabel .
    ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
}
WHERE {
    {
        SELECT DISTINCT ?uri WHERE {
            ?uri a skos:Concept ;
                skos:inScheme <https://data.cultureelerfgoed.nl/term/id/cht/b532325c-dc08-49db-b4f1-15e53b037ec3> ;
                ?predicate ?label .

            VALUES ?predicate { skos:prefLabel skos:altLabel skos:hiddenLabel }

            FILTER(CONTAINS(LCASE(?label), LCASE(?query)))

            {
                VALUES ?genreUri { ?genres }
                # Genre-to-top-concept mapping for hierarchy-restricted genres
                VALUES (?genreUri ?topConcept) {
                    (<https://data.cultureelerfgoed.nl/termennetwerk/onderwerpen/Materialen> <https://data.cultureelerfgoed.nl/term/id/cht/aa872ce6-a74c-4f81-96ec-6ee0e717f92a>)
                    (<https://data.cultureelerfgoed.nl/termennetwerk/onderwerpen/Stijlen> <https://data.cultureelerfgoed.nl/term/id/cht/63cca950-f545-467a-9d70-db3a2b21bba3>)
                    (<https://data.cultureelerfgoed.nl/termennetwerk/onderwerpen/Periodes> <https://data.cultureelerfgoed.nl/term/id/cht/63cca950-f545-467a-9d70-db3a2b21bba3>)
                }
                ?uri skos:broader+ ?topConcept .
            }
            UNION
            {
                VALUES ?genreUri { ?genres }
                # All other genres: no subtree restriction
                FILTER(?genreUri NOT IN (
                    <https://data.cultureelerfgoed.nl/termennetwerk/onderwerpen/Materialen>,
                    <https://data.cultureelerfgoed.nl/termennetwerk/onderwerpen/Stijlen>,
                    <https://data.cultureelerfgoed.nl/termennetwerk/onderwerpen/Periodes>
                ))
            }
        }
        #LIMIT#
    }
    OPTIONAL {
        ?uri skos:prefLabel ?prefLabel .
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
    }
    OPTIONAL {
        ?uri skos:hiddenLabel ?hiddenLabel .
    }
    OPTIONAL {
        ?uri skos:scopeNote ?scopeNote .
    }
    OPTIONAL {
        ?uri skos:definition ?scopeNote .
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
    }
    OPTIONAL {
        ?uri skos:related ?related_uri .
        ?related_uri skos:prefLabel ?related_prefLabel .
   }
    OPTIONAL {
        ?uri skos:exactMatch ?exactMatch_uri .
        FILTER(!STRSTARTS(STR(?exactMatch_uri), "https://data.cultureelerfgoed.nl/term/id/rn"))
        FILTER(!STRSTARTS(STR(?exactMatch_uri), "https://data.cultureelerfgoed.nl/semnet"))
        OPTIONAL {
            ?exactMatch_uri skos:prefLabel ?exactMatch_prefLabel .
        }
    }
}
